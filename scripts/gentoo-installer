#!/usr/bin/env perl

use strict;
use warnings;

use Getopt::Std;
use IO::Handle;
use Term::ReadKey;

# select((select(STDOUT), $| = 1)[0]);
STDOUT->autoflush(1);

# This script should automate as much of the Gentoo installation as
# possible. For reference, see
#   https://wiki.gentoo.org/wiki/Handbook:AMD64/

#     getopts('3d:i:ns:w', \%opts);
# Options:
#   -3			unpack the stage3 hardened tarball
#   -d device		specify the root disk
#   -i interface	specify the network interface
#   -n			network is already setup
#   -s
#   -w			interface is wireless

# run a shell command; if it exits with a non-zero status, print the
# eulogy and die.
sub shell_or_die {
    my ($cmd, $eulogy) = @_;
    printf("\t\$ $cmd\n");
    system("$cmd") && die "[!] $eulogy";
}

sub getpass {
    print "@_";
    Term::ReadKey::ReadMode('noecho');
    chomp(my $password = Term::ReadKey::ReadLine(0));
    Term::ReadKey::ReadMode('normal');
    print "\n";
    return $password;
}

sub getpass_confirm {
    my $prompt = shift;
    my $password = getpass("\t$prompt");
    my $confirm_password = getpass("\tconfirm password: ");

    if ($confirm_password eq $password) {
	return $password;
    }

    print "[!] passwords did not match.\n";
    getpass_confirm($prompt);
}

sub encrypt_disk {
    my $cryptdev = shift;

    print "[+] encrypting $cryptdev\n";
    my $disk_pass = getpass_confirm("disk encryption password: ");

    # use system() to avoid printing the password to screen
    print "\tcryptsetup --use-urandom -q luksFormat $cryptdev -\n";
    system("echo -n \"$disk_pass\" | cryptsetup --use-urandom -q luksFormat $cryptdev -") &&
	die "cryptsetup failed";
    sleep(1); # hopefully stabilises the cryptsetup ioctl issue
    print "\tcryptsetup -q luksOpen $cryptdev lvm -\n";    
    system("echo -n \"$disk_pass\" | cryptsetup -q luksOpen $cryptdev lvm -") &&
	die "failed to open crypt device";
}

# Partition the disks, encrypt the root drive, create the necessary
# file systems, and mount everything/activate swap.
sub disk_setup {
    my $disk = shift;
    my $bootdev = $disk . "1";
    my $cryptdev = $disk . "2";
    print "\tboot device:  $bootdev\n";
    print "\tcrypt device: $cryptdev\n";

    # The following are the commands to set up a 256M boot partition
    # and an LVM partition taking up the rest of the disk. I typed
    # these out in an interactive fdisk session to make sure they were
    # right.
    my $fdisk_cmd = <<"END";
o
n
p
1
2048
+256M
n
p
2


t
2
8e
w
END
    shell_or_die("echo \"$fdisk_cmd\" | fdisk $disk",
		 "failed to partition disk");

    encrypt_disk($cryptdev);
    shell_or_die("mkfs.ext3 $bootdev");
    shell_or_die("pvcreate /dev/mapper/lvm", "failed to setup PV");
    shell_or_die("vgcreate vol0 /dev/mapper/lvm", "failed to set up VG");
    shell_or_die("lvcreate --name lv_swap -L 12GB vol0",
		 "failed to set up swap LV");
    shell_or_die("lvcreate --name lv_var -L 20GB vol0",
		 "failed to set up /var LV");
    shell_or_die("lvcreate --name lv_root -l 100%FREE vol0",
		 "failed to set up / LV");
    shell_or_die("mkswap /dev/mapper/vol0-lv_swap",
		 "mkswap failed");
    shell_or_die("swapon /dev/mapper/vol0-lv_swap",
		 "failed to activate swap");
    shell_or_die("mkfs.ext4 /dev/mapper/vol0-lv_root",
		 "failed to format / VG with ext4");
    shell_or_die("mkfs.ext4 /dev/mapper/vol0-lv_var",
		 "failed to format /var VG with ext4");
    shell_or_die("mount /dev/mapper/vol0-lv_root /mnt/gentoo",
		 "failed to mount root");
    shell_or_die("mkdir /mnt/gentoo/var && mount /dev/mapper/vol0-lv_var /mnt/gentoo/var",
		 "failed to mount /var");
    shell_or_die("mkdir /mnt/gentoo/boot && mount $bootdev /mnt/gentoo/boot",
		 "failed to mount /boot");    
}

# show a prompt, read a line of input.
sub read_line {
    print "@_: ";
    chomp(my $answer = <>);

    return $answer;
}

# I can never remember the syntax, so I told it to Perl so it wouldn't
# forget.
sub generate_wpa_conf {
    my $ssid = read_line("SSID");
    my $passphrase = read_line("WPA passphrase");
    
    length $ssid || die '[!] no SSID provided.';
    length $passphrase || die '[!] no WPA passphrase provided';
    
    open(my $wpa_conf, '>/etc/wpa_supplicant.conf') || 
	die '[!] failed to open wpa_supplicant config';
    print $wpa_conf <<"END_CONF";
ctrl_interface=/var/run/wpa_supplicant

network={
	ssid="$ssid"
	key_mgmt=WPA-PSK
	proto=WPA
	psk="$passphrase"
}
END_CONF
	close($wpa_conf);
}

# If this is a wireless setup, create the wpa_supplicant
# configuration. Run DHCP on the interface.
sub setup_network {
    print "[+] setting up networking\n";
    my ($interface, $wireless) = @_;
    
    if (!defined($interface)) {
	$interface = "eth0";
    }

    if ($wireless) {
	generate_wpa_conf();
	shell_or_die("wpa_supplicant -B -i$interface -c/etc/wpa_supplicant.conf",
		     "failed to start wpa_supplicant");
    }

    shell_or_die("dhcpcd $interface", "failed to acquire a DHCP lease");
}

sub main {
    my %opts = (
	3 => 0,             # 3: look for and unpack the stage3 tarball
	d => '/dev/sda',    # d: specify the root device
	i => '',            # i: network interface
	n => 0,             # n: network is already setup
	s => '',            # s: specify a root filesystem to unpack
	w => 0,             # w: network interface is a wireless card
    );
    getopts('3d:i:ns:w', \%opts);

    # 1. Set up networking.
    if (defined($opts{n}) && !$opts{n}) {
	setup_network($opts{i}, $opts{w});
    }

    # 2. Set up disks.
    if (!defined($opts{d})) {
	$opts{d} = "/dev/sda";
    }

    print "[+] setting up disks\n";
    disk_setup($opts{d});

    # 3. Unpack stage3 or rootfs
    if (defined($opts{s}) && length $opts{s}) {
	print "[+] unpacking rootfs\n";
	shell_or_die("tar xvjpf $opts{s} --xattrs -C /mnt/gentoo",
		     "failed to unpack rootfs");
    } elsif (defined($opts{3}) && $opts{3}) {
	print "[+] unpacking stage3\n";
	my @stage3 = <"/mnt/livecd/stage3*hardened*.tar.bz2">;
	length scalar(@stage3) || die "[!] couldn't find a suitable stage3";
	my $stage3 = shift @stage3;
	shell_or_die("tar xvjpf $stage3 --xattrs -C /mnt/gentoo",
		     "failed to unpack stage3");
    }

    # 4. Time to run the post-installer.
    print "[+] finalising base system\n";
    shell_or_die("cp /mnt/livecd/gentoo-postinstall /mnt/gentoo/usr/bin/gentoo-postinstall",
		 "failed to copy post-install script");

    my $inst_type = "";
    if (defined($opts{s}) && length $opts{s}) {
	$inst_type = "stage4";
    } elsif (defined($opts{3}) && $opts{3}) {
	$inst_type = "stage3";
    }
    
    shell_or_die("/mnt/gentoo/usr/bin/gentoo-postinstall $inst_type",
		 "post-installer failed");


    # TODO:
    # + mount the GnuPG keyring files
    # + decrypt the base file system (which would let me skip the
    #   stage3 option above)
    # + unpack the base file system
}

main();
